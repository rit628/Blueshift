float max(float x, float y) {
    if (x > y) {
        return x;
    }
    return y;
}

oblock quadrantLighter(MOUSE ms, float xBorder, float yBorder, LIGHT q1, LIGHT q2, LIGHT q3, LIGHT q4) : 
triggerOn(ms),
processPolicy({
    ms : {
        "read" : "any"
    }
})
{
    q1.on = false;
    q2.on = false;
    q3.on = false;
    q4.on = false;
    
    if (ms.x > xBorder / 2) {
        if (ms.y > yBorder / 2) {
            print("IN BOTTOM RIGHT");
            q4.on = true;
        }
        else {
            print("IN TOP RIGHT");
            q1.on = true;
        }
    }
    else {
        if (ms.y > yBorder / 2) {
            print("IN BOTTOM LEFT");
            q3.on = true;
        }
        else {
            print("IN TOP LEFT");
            q2.on = true;
        }
    }
}

oblock calibrateMouse(KEYBOARD calibrationSig, MOUSE ms, float maxX, float maxY) :
triggerOn(calibrationSig),
processPolicy({
    ms : {
        "read" : "any"
    },
    calibrationSig : {
        "read" : "any"
    }
})
{
    print("Calibrating...");
    maxX = max(maxX, ms.x);
    maxY = max(maxY, ms.y);
    print("New window maxes: ", toString(maxX), toString(maxY));
}

setup() {
    KEYBOARD KBD = "localhost::mode-all";
    MOUSE MS = "localhost::mode-all";

    virtual float maxX = 0;
    virtual float maxY = 0;

    calibrateMouse(KBD, MS, maxX, maxY);

    LIGHT Q1 = "rpi::PIN-21";
    LIGHT Q2 = "rpi::PIN-26";
    LIGHT Q3 = "rpi::PIN-19";
    LIGHT Q4 = "rpi::PIN-20";

    quadrantLighter(MS, maxX, maxY, Q1, Q2, Q3, Q4);    
}